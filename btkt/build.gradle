apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.moowork.node'
apply plugin: 'maven-publish'

kotlin {
    targets {
        all {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    allWarningsAsErrors = true
                }
            }
            mavenPublication {
                pom {
                    name = 'BT.kt'
                    description = 'A Kotlin library that provides extensions for bit-twiddling'
                    url = 'https://github.com/tmurakami/btkt'
                    inceptionYear = '2018'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'tmurakami'
                            name = 'Tsuyoshi Murakami'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/tmurakami/btkt.git'
                        developerConnection = 'scm:git:ssh://github.com/tmurakami/btkt.git'
                        url = 'https://github.com/tmurakami/btkt'
                    }
                }
            }
        }
        fromPreset(presets.js, 'js') {
            compilations.main {
                tasks[compileKotlinTaskName].kotlinOptions {
                    sourceMap = true
                    sourceMapEmbedSources = 'always'
                    moduleKind = 'umd'
                }
            }
            compilations.test {
                tasks[compileKotlinTaskName].kotlinOptions {
                    sourceMap = true
                    moduleKind = 'commonjs'
                }
            }
        }
        fromPreset(presets.jvm, 'jvm') {
            compilations.main {
                tasks[compileKotlinTaskName].kotlinOptions {
                    freeCompilerArgs += ['-Xno-param-assertions']
                }
            }
        }
        def host = org.jetbrains.kotlin.konan.target.HostManager.host
        def nativeTarget = presets.find { it.properties.konanTarget == host }.with {
            fromPreset(it, it.name) {
                mavenPublication {
                    artifactId = "$project.name-${konanTarget.name.replace('_', '-')}"
                }
                compilations.all {
                    kotlinSourceSets.each {
                        it.kotlin.srcDirs = ["src/native${name.capitalize()}/kotlin"]
                    }
                }
            }
        }
        if ((System.env.CI ?: 'false').toBoolean()) {
            presets.matching { it.hasProperty('konanTarget') && it.konanTarget != host }.all {
                fromPreset(it, it.name) {
                    mavenPublication {
                        artifactId = "$project.name-${konanTarget.name.replace('_', '-')}"
                    }
                    compilations.all {
                        nativeTarget.compilations[name].kotlinSourceSets.each { source it }
                    }
                }
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
    }
}

task populateNodeModules(type: Copy, dependsOn: compileKotlinJs) {
    from compileKotlinJs.destinationDir
    doFirst {
        configurations.jsTestRuntimeClasspath.each { from zipTree(it).matching { include '*.js' } }
    }
    into "$buildDir/node_modules"
}

node {
    version = versions.node
    download = true
}
// TODO https://github.com/srs/gradle-node-plugin/issues/301
repositories.withType(IvyArtifactRepository) { metadataSources { artifact() } }

task installJsTestDependencies(type: NpmTask) {
    inputs.file 'package-lock.json'
    outputs.dir 'node_modules'
    args = ['install',
            'mocha',
            'source-map-support',
            'mocha-multi-reporters',
            'mocha-junit-reporter',
            'mochawesome']
}

task jsTest(type: NodeTask,
            overwrite: true,
            dependsOn: [compileTestKotlinJs, populateNodeModules, installJsTestDependencies]) {
    inputs.files compileKotlinJs,
                 compileTestKotlinJs,
                 populateNodeModules,
                 installJsTestDependencies
    def resultsDir = "$testResultsDir/jsTest"
    def reportDir = "$testReportDir/jsTest"
    outputs.dirs resultsDir, reportDir
    script = file('node_modules/mocha/bin/mocha')
    def multiReportersConfig = file("$buildDir/mocha-multi-reporters-config.json")
    doFirst {
        multiReportersConfig.text =
                """|{
                   |  "reporterEnabled": "mocha-junit-reporter,mochawesome",
                   |  "mochaJunitReporterReporterOptions": {
                   |    "suiteTitleSeparatedBy": ".",
                   |    "mochaFile": "$resultsDir/TEST-${project.group}.${project.name}.xml"
                   |  },
                   |  "mochawesomeReporterOptions": {
                   |    "quiet": true,
                   |    "reportDir": "$reportDir",
                   |    "reportFilename": "index"
                   |  }
                   |}
                   |""".stripMargin()
    }
    args = [compileTestKotlinJs.outputFile,
            '--require', 'source-map-support/register',
            '--reporter', 'mocha-multi-reporters',
            '--reporter-options', "configFile=$multiReportersConfig"]
}
check.dependsOn jsTest
