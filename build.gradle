buildscript {
    ext.versions = [kotlin: '1.3.21',
                    ktlint: '0.31.0',
                    node  : '10.13.0',]
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.1'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /([-\/:])$v([-\/'])/ },
                           replacement: { v, p -> "\$1$v\$2" }]
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /(\/Kotlin-)[\d.]+/ },
                           replacement: { v, p -> "\$1${versions.kotlin}" }]
        pre 'commit'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    version = scmVersion.version
    group = 'com.github.tmurakami.btkt'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}
